// Execute Tasks in SubModule: gradle MODUL:clean build
plugins {
    id "de.freese.gradle.conventions" version "$version_myConventionPlugin" apply false
    id "io.spring.dependency-management" version "$version_springDependencyManagement" apply false
    id "org.springframework.boot" version "$version_springBoot" apply false
    id "name.remal.sonarlint" version "$version_nameRemalSonarlint" apply false
    id "de.freese.gradle.sonarlint-conventions" version "$version_myConventionPlugin" apply false
}

subprojects {
    apply plugin: "base"
    apply plugin: "de.freese.gradle.conventions"
    apply plugin: "io.spring.dependency-management"
    // apply plugin: "name.remal.sonarlint"
    // apply plugin: "de.freese.gradle.sonarlint-conventions"

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$version_springBoot")
        }

        dependencies {
            dependency("commons-cli:commons-cli:$version_commonsCli")
        }
    }

//    afterEvaluate { project ->
//        if (project.plugins.hasPlugin("java") || project.plugins.hasPlugin("java-library") || project.plugins.hasPlugin("application")) {
//            dependencies {
//                testImplementation("org.junit.jupiter:junit-jupiter")
//                testRuntimeOnly("org.junit.platform:junit-platform-launcher")
//            }
//        }
//    }
}

project("jsync-core") {
    apply plugin: "java-library"
    description = "A Java rsync clone: Core Module"
    dependencies {
        api("io.projectreactor:reactor-core")
        api("org.slf4j:slf4j-api")
    }
}

project("jsync-console") {
    apply plugin: "java"
    description = "A Java rsync clone: Console Module"
    dependencies {
        implementation(project(":jsync-core"))

        implementation("commons-cli:commons-cli")

        runtimeOnly("org.slf4j:slf4j-nop")
    }
}

project("jsync-remote-rsocket") {
    apply plugin: "java-library"
    description = "A Java rsync clone: RSocket Module"
    dependencies {
        api(project(":jsync-core"))

        api("io.rsocket:rsocket-transport-local")
        api("io.rsocket:rsocket-transport-netty")
    }
}

project("jsync-remote-rsocket-server") {
    apply plugin: "java-library"
    apply plugin: "org.springframework.boot"
    description = "A Java rsync clone: RSocket-Server Module"
    dependencies {
        api(project(":jsync-remote-rsocket"))

        runtimeOnly("ch.qos.logback:logback-classic")
    }
    // Start: gradle bootRun --args="--spring.profiles.active=dev"
    // The archive name. If the name has not been explicitly set, the pattern for the name is:
    // [archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
    // archiveFileName = "my-boot.jar"
    springBoot {
        mainClass = "de.freese.jsync.rsocket.server.JSyncRSocketServer"
    }
    // gradle bootRun --args="--spring.profiles.active=Server,HsqldbEmbeddedServer --server.port=65111"
    // gradle bootRun Dspring-boot.run.arguments="65111"
    bootRun {
        args = [65111]
        jvmArgs = ["-Xms32m", "-Xmx512m", "-XX:TieredStopAtLevel=1", "-Djava.security.egd=file:/dev/./urandom"]
        // -Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005
    }
}

project("jsync-remote-nio") {
    apply plugin: "java-library"
    description = "A Java rsync clone: NIO Module"
    dependencies {
        api(project(":jsync-core"))
    }
}

project("jsync-remote-nio-server") {
    apply plugin: "java-library"
    description = "A Java rsync clone: NIO-Server Module"
    dependencies {
        api(project(":jsync-remote-nio"))

        runtimeOnly("ch.qos.logback:logback-classic")
    }
}

project("jsync-swing") {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    description = "A Java rsync clone: Swing-GUI"
    dependencies {
        implementation(project(":jsync-remote-rsocket"))
        implementation(project(":jsync-remote-nio"))

        runtimeOnly("ch.qos.logback:logback-classic")
    }
    // Start: gradle bootRun --args="--spring.profiles.active=dev"
    // The archive name. If the name has not been explicitly set, the pattern for the name is:
    // [archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
    // archiveFileName = "my-boot.jar"
    springBoot {
        mainClass = "de.freese.jsync.swing.JSyncSwingLauncher"
    }
    // gradle bootRun --args="--spring.profiles.active=Server,HsqldbEmbeddedServer --server.port=65111"
    // gradle bootRun Dspring-boot.run.arguments="65111"
    bootRun {
//        args = [
//                "--spring.profiles.active=Server,HsqldbEmbeddedServer"
//                , "--server.port=65111"
//        ]
        jvmArgs = ["-Xms32m", "-Xmx512m", "-XX:TieredStopAtLevel=1", "-Djava.security.egd=file:/dev/./urandom"]
        // -Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005
    }
}

project("jsync-test") {
    apply plugin: "java"
    description = "A Java rsync clone: Test Module"
    dependencies {
        testImplementation(project(":jsync-remote-rsocket-server"))
        testImplementation(project(":jsync-remote-nio-server"))

        testImplementation("io.projectreactor:reactor-test")
        testImplementation("org.junit.jupiter:junit-jupiter")

        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }
//    test {
//        maxParallelForks = 1 // Will not do parallel execution
//    }
}
